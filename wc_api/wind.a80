;”г­ЄжЁЁ а Ў®вл б ®Є®­­®© бЁбвҐ¬®© ¤«п бв ­¤ авЁ§ жЁЁ
		MODULE	wind

init		PUSH	AF
		LD	A,15
		CALL	#6006
		POP	AF
		RET

printw		LD	(wind.current_sow),IX
		PUSH	AF
		LD	A,1
		CALL	#6006
		LD	A,(IX+6)	;цвет окна
		LD	(current_attr),A
		POP	AF
		RET

closew		PUSH	AF
		LD	IX,(wind.current_sow)
		LD	A,2
		CALL	#6006		
		XOR	A
		LD	(wind.current_sow),A
		LD	(wind.current_sow+1),A
		POP	AF
		RET

;turn on text cursoe
cur_on		PUSH	AF
		PUSH	DE
		PUSH	HL
		LD	A,1
		LD	(cursor_enabled),A
		CALL	wind.cur_prn
		POP	HL
		POP	DE
		POP	AF
		RET

;Сервисная ф-ция.для восстановления аттрибута под курсором
;IN:
; IX - SOW
; D,E - Y,X
cur_off_tmp	PUSH	AF
		PUSH	HL
		LD	A,5		;5¦GADRW - получение адреса в окне
		CALL	#6006
		SET	7,L
		LD	A,(cursor_lattr) ;restore color under cursor
		LD	(HL),A
		POP	HL
		POP	AF
		RET

;Печать курсора
;Портит регистры IS, DE, HL,AF
cur_prn		LD	IX,(wind.current_sow)
		LD	E,(IX+12)	;X позиция курсора в окне (от 1)
		LD	D,(IX+13)	;Y нижний ограничитель
cur_prn_a	LD	A,5		;5¦GADRW - получение адреса в окне
		CALL	#6006
		SET	7,L
		LD	A,(HL)
		LD	(cursor_lattr),A
		XOR	#FF
		LD	(HL),A
		RET

;turn off text cursoe
cur_off		PUSH	AF
		PUSH	DE
		PUSH	HL

		XOR	A
		LD	(cursor_enabled),A
		LD	IX,(wind.current_sow)
		LD	E,(IX+12)	;X позиция курсора в окне (от 1)
		LD	D,(IX+13)	;Y нижний ограничитель
		CALL	wind.cur_off_tmp
		POP	HL
		POP	DE		
		POP	AF
		RET

;- print one symbol
; IN: A - char
printc		PUSH	IX
		PUSH	BC
		PUSH	DE
		PUSH	HL
		LD	IX,(wind.current_sow)
		LD	E,(IX+12)	;X позиция курсора в окне (от 1)
		LD	D,(IX+13)	;Y нижний ограничитель
		PUSH	AF
		LD	A,(wind.cursor_enabled)
		OR	A
		JR	Z,7f
		CALL	cur_off_tmp
7		POP	AF
		CP	13		;CR + LF
		JR	Z,1f
		CP	10		;LF
		JR	Z,4f
		CP	8		;Cursor to left
		JR	NZ,5f
		LD	A,E
		OR	A
		JR	Z,3f
		DEC	A
		LD	(IX+12),A
		JR	3f
5		PUSH	AF
		LD	A,5		;5¦GADRW - получение адреса в окне
		CALL	#6006
		POP	AF
		LD	(HL),A
		SET	7,L		;Аттрибут
		LD	A,(current_attr)
		LD	(HL),A
		LD	A,(IX+4)	;ширина окна (Width)
		SUB	2
		CP	E
		JR	Z,1f
		INC	E
		LD	(IX+12),E	;X позиция курсора в окне (от 1)
		JR	3f
1		LD	A,1
		LD	(IX+12),A	;X позиция курсора в окне (от 1)
4		LD	A,(IX+5)	;высота окна (Height)
		SUB	2
		CP	D
		JR	Z,2f
		INC	D
		LD	(IX+13),D	;Y position
		JR	3f
2		LD	A,84		;¦SCRLWOW
		LD	DE,#0101	;X,Y = 1. Не трогаем рамку
		LD	C,(IX+4)	;ширина окна (Width)
		LD	B,(IX+5)	;высота окна (Height)
		DEC	C
		DEC	C
		DEC	B
		DEC	B						
		EXA
		LD	A,#C1
;		LD	A,#01
		EXA
		CALL	#6006
;		CALL	wind.scroll_area_up
3		LD	A,(wind.cursor_enabled)
		OR	A
		CALL	NZ,cur_prn		;Печать курсора, если он включен
		POP	HL
		POP	DE
		POP	BC
		POP	IX
		RET

;IN: HL - string
;OUT: HL - EOF
prints		PUSH	AF
1		LD	A,(HL)
		OR	A
		JR	Z,2f
		CALL	wind.printc
		INC	HL
		JR	1b
2		POP	AF
		RET

a_hex		PUSH	HL
		PUSH	AF
		EXA
		LD	HL,wind.bufer
		LD	A,10		;NORK A - to hex. перевод байта в HEX (текстовый формат)
		CALL	#6006		
		XOR	A
		LD	(wind.bufer+2),A
		LD	HL,wind.bufer
		CALL	wind.prints
		POP	AF
		POP	HL
		RET

hl_hex		PUSH	BC
		PUSH	HL
		POP	BC
		LD	A,B
		EXA
		LD	HL,bufer
		LD	A,10		;NORK A - to hex
		CALL	#6006		
		LD	A,C
		EXA
		LD	HL,bufer+2
		LD	A,10		;NORK A - to hex
		CALL	#6006		
		XOR	A
		LD	(bufer+4),A
		LD	HL,bufer
		CALL	prints
		POP	BC
		RET

;Set attributes
; IN: A - ATTR
set_attr	LD	(current_attr),A
		RET

cursor_enabled	DB	0
cursor_lattr	DB	0	;цыет, который был под курсором для восстановления
current_attr	DB	0
bufer		DS	5
current_sow	DW	0	;current window
		ENDMODULE